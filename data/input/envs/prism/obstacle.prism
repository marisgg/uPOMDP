pomdp

observables
start
endobservables

const int N=6;
const int axMAX = N-1;
const int ayMAX = N-1;
const int axMIN = 0;
const int ayMIN = 0;
const double sl;
const int ob1x = axMAX-1;
const int ob1y = ayMAX-1;
const int ob2x = axMAX;
const int ob2y = ayMIN+1;
const int ob3x = axMIN+1;
const int ob3y = ayMIN;
const int ob4x = axMAX;
const int ob4y = ayMAX-1;
const int ob5x = axMAX-3;
const int ob5y = ayMAX-1;

formula done = start & ax = axMAX & ay = ayMAX;
observable "amdone" = done;
formula crash =  (ax = ob1x & ay = ob1y) | (ax = ob2x & ay = ob2y)  | (ax = ob3x & ay = ob3y) | (ax = ob4x & ay = ob4y) | (ax = ob5x & ay = ob5y)  ;
observable "hascrash" = crash;

module robot
    ax : [axMIN..axMAX] init 0;
    ay : [ayMIN..ayMAX] init 0;
    start : bool init false;
    [placement] !start ->  1/4: (ax'=ob1x-1) & (ay'=ob1y) & (start'=true) + 1/4: (ax'=1) & (ay'=1) & (start'=true) + 1/4: (ax'=2) & (ay'=1) & (start'=true) + 1/4: (ax'=1) & (ay'=3) & (start'=true);

    [west] start -> (1-sl): (ax'=max(ax-1,axMIN)) + sl: (ax'=max(ax-2,axMIN));
    [east] start -> (1-sl): (ax'=min(ax+1,axMAX)) + sl: (ax'=min(ax+2,axMAX));
    [south]  start -> (1-sl): (ay'=min(ay+1,ayMAX)) + sl: (ay'=min(ay+2,ayMAX));
    [north]  start -> (1-sl): (ay'=max(ay-1,ayMIN)) + sl: (ay'=max(ay-2,ayMIN));
    [done] done -> true;
endmodule


label "goal" = done;
label "traps" = crash;
label "notbad" =  !crash;

rewards "Cost"
    done = false : 1;
    crash = true:100;
endrewards

